

// PURPOSE: represents a moving platform that user controls
class Platform {

    // coordinates of a platform
    field int x, y;

    // size of a platform
    static int X_SIZE;
    static int Y_SIZE;

    // movement speed of a platform
    static int SPEED;


    // PURPOSE: constructs a new platform with given coordinates
    // CHANGES: memory
    constructor Platform new(int Ax, int Ay) {

        do init();

        let x = Ax;
        let y = Ay;

        do draw();
        
        return this;
    }


    // PURPOSE: initializes constants for a platform
    // CHANGES: constants
    method void init() {

        let X_SIZE = 30;
        let Y_SIZE = 5;
        let SPEED = 6;

        return;
    }


    // PURPOSE: getters
    // RETURNS: fields of current object
    method int getX() { return x; }
    method int getY() { return y; }

    
    // PURPOSE: cleans memory from Platform object
    // CHANGES: memory
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    
    // PURPOSE: draws platform on the screen
    // CHANGES: screen
    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + X_SIZE, y + Y_SIZE);
        return;
    }


    // PURPOSE: erases platform from the screen
    // CHANGES: screen
    method void erase() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + X_SIZE, y + Y_SIZE);
        return;
    }


    // PURPOSE: moves platform to the left
    // CHANGES: x, screen

    // TODO: change draw logic to possible perfomance increase

    method void moveLeft(int left_border) {
        if (x > left_border) {
            do erase();
            let x = x - SPEED;
            do draw();
        }
        return;
    }


    // PURPOSE: moves platform to the right
    // CHANGES: x, screen
    
    // TODO: change draw logic to possible perfomance increase

    method void moveRight(int right_border) {
        if (x + X_SIZE < right_border) {
            do erase();
            let x = x + SPEED;
            do draw();
        }
        return;
    }

} // class