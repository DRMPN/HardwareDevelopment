

// PURPOSE: represents a breakable break
class Brick {
    
    // coordinates of a brick
    field int x1, y1, x2, y2;


    // size of a brick
    static int X_SIZE;
    static int Y_SIZE;


    // PURPOSE: creates a new brick at given coordinates
    constructor Brick new(int Ax, int Ay) {

        do init();

        let x1 = Ax;
        let x2 = Ax + X_SIZE;

        let y1 = Ay;
        let y2 = Ay + Y_SIZE;

        do draw();
        
        return this;
    }


    // PURPOSE: initializes constants for a brick
    method void init() {
        let X_SIZE = 15;
        let Y_SIZE = 8;
        return;
    }


    // PURPOSE: getters
    // RETURNS: fields of a current object
    method int getX1() { return x1; }
    method int getX2() { return x2; }
    method int getY1() { return y1; }
    method int getY2() { return y2; }


    // PURPOSE: cleans memory from a brick object
    // CHANGES: memory
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }


    // PURPOSE: draws a brick on a screen
    // CHANGES: screen
    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x1, y1, x2, y2);
        return;
    }


    // PURPOSE: erases a brick from a screen
    // CHANGES: screen
    method void erase() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x1, y1, x2, y2);
        return;
    }


}